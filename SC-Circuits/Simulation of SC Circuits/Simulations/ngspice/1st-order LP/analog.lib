* Library of macromodel for analog IC design
* (c) C. Enz and F. Krummenacher, 2020
* Electronics Lab, EPFL, Lausanne, Switzerland

* VDD and VSS supplies
.subckt supplies VDD 0 VSS
VDD VDD 0 {VDD}
VSS 0 VSS {VSS}
.ends supplies

* Current-controlled voltage source (CCVS)
.subckt CCVS out+ out- Sense+ Sense-
H1 out+ out- V1 {Rm}
V1 sense+ sense-  0V
.ends CCVS

* Current-controlled current source (CCCS)
.subckt CCCS out+ out- sense+ sense-
F1 out+ out- V1 {Ai}
V1 sense+ sense-  0V
.ends CCCS

* Ideal single-ended op-amp
.subckt Av in+ in- out
E1 out 0 in+ in- {A0}
.ends Av

* Ideal single-ended op-amp
.subckt OPAMP in+ in- out
E1 out 0 in+ in- {A0}
.ends Av

* Ideal fully-differential op-amp
.subckt Av_diff in+ in- out+ out-
E1 out+ out- in+ in- {A0}
Ro1 Out+ 0 1T
Ro2 Out- 0 1T
.ends Av_diff

* Ideal fully-differential op-amp
.subckt Av_diff_cm in+ in- out+ out- out_cm
E1 out+ out_cm in+ in- {A0/2}
E2 out- out_cm in+ in- {-A0/2}
.ends Av_diff_cm

* Ideal single-ended transconductor
.subckt Gm in+ in- out
G1 0 out in+ in- {Gm}
Ro out 0 1T
.ends Gm

* Ideal fully-differential transconductor
.subckt Gm_diff in+ in- out+ out-
G1 out- out+ in+ in- {Gm}
Ro1 out+ 0 1T
Ro2 out- 0 1T
.ends Gm_diff

* Ideal fully-differential transconductor
.subckt Gm_diff_cm in+ in- out+ out- out_cm
G1 out_cm out+ in+ in- {Gm}
G2 out_cm out- in+ in- {-Gm}
Ro1 out+ out_cm 1T
Ro2 out- out_cm 1T
.ends Gm_diff_cm


* 1/f noise voltage source for AC noise analysis:
* -----------------------------------------------

.subckt Vnf n1 n2
* Parameters:
* Vnat1Hz:	value of the PSD at 1Hz in V/sqrt(Hz)
* flow:		lower frequency above which the 1/f shape is valid
Rnf 1 0 {60E18*Vnat1Hz*Vnat1Hz}
Inf 1 0 0 ; dummy source to have more than 1 element at node 1
Enf n1 n2 Laplace {V(1)} = {(1+sqrt(1/flow))/(1+sqrt(s/(2*Pi*flow)))}
.ends

* 1/f and white noise voltage source for AC noise analysis:
* ---------------------------------------------------------
* The noise voltage gives a PSD that is constant up to flow,
* then decreases as 1/f until fcorner, then stays constant up to fhigh
* after which it decreases as 1/f^2. The parameters are:
* the white noise level (between fcorner and fhigh), the corner
* frequency fcorner and the high frequency cut-off frequency fhigh

.subckt Vn n1 n2
* Parameters:
* Vnwhite:	value of the PSD in the constant part between fcorner and fhigh
* flow:		frequency above which the 1/f shape is valid
* fcorner:	frequency at which the 1/f noise is equal to the white noise
* fhigh:	cut-off frequency
Rn 1 0 {60E18*Vnwhite*Vnwhite}
In 1 0 0 ; dummy source to have more than 1 element at node 1
En n1 n2 Laplace {V(1)} = {sqrt(fcorner/flow)*(1+sqrt(s/(2*pi*fcorner)))/((1+sqrt(s/(2*Pi*flow)))*(1+s/(2*Pi*fhigh)))}
.ends

* Diff. transconductor with linear V/I characteristics:
* -----------------------------------------------------
.subckt OTAlin in+ in- out+ out-
* Parameters:
* Gm=10u	; transconductance                              [A/V]
* A0=1000	; Dc gain	                                [A/V]
* Visat=100m	; input saturation voltage                        [A]
* Vios=0	; equivalent input offset                         [V]
* Cid=10f	; differential input parasitic capacitance        [F]
* Cic=10f	; input parasitic capacitance to ground           [F]
* fp=0		; parasitic frequency				  [s]
* gamma=2	; broadband noise factor (gm*Rni)                 [-]

.param Go={Gm/A0}
.param fp=100MEG
.param flow=1m
.param Vnat1Hz={sqrt(16E-21*fcorner*gamma/Gm)}

Cid in+ in- {Cid}
Cic1 in+ 0 {Cic}
Cic2 in- 0 {Cic}
Btp 0 1 I=Gm/gamma*(V(in+,in-)-Vios)
Rn 1 0 {gamma/Gm}
Cp 1 0 {Gm/(2*pi*fp*gamma)}
Xnf 1 2 Vnf Vnat1Hz={Vnat1Hz} flow={flow}
Bdm out- out+ I=Gm*limit(V(2),-Visat,Visat)
Ro1 out+ 0 {1/(2*Go)}
Ro2 out- 0 {1/(2*Go)}
.ends


* Diff. transconductor with hyperbolic tangent V/I characteristics:
* -----------------------------------------------------------------
.subckt OTAtanh in+ in- out+ out-
* Parameters:
* Gm=10u	; transconductance                              [A/V]
* A0=1000	; Dc gain	                                [A/V]
* Visat=80m	; input saturation voltage                        [A]
* Vios=0	; equivalent input offset                         [V]
* Cid=10f	; differential input parasitic capacitance        [F]
* Cic=10f	; input parasitic capacitance to ground           [F]
* fp=0		; parasitic frequency				  [s]
* gamma=2	; broadband noise factor (gm*Rni)                 [-]

.param Go={Gm/A0}
.param fp=100MEG
.param flow=1m
.param Vnat1Hz={sqrt(16E-21*fcorner*gamma/Gm)}

Cid in+ in- {Cid}
Cic1 in+ 0 {Cic}
Cic2 in- 0 {Cic}
Btp 0 1 I=Gm/gamma*(V(in+,in-)-Vios)
Rn 1 0 {gamma/Gm}
Cp 1 0 {Gm/(2*pi*fp*gamma)}
Xnf 1 2 Vnf Vnat1Hz={Vnat1Hz} flow={flow}
Bdm out- out+ I=Gm*Visat*tanh(V(2)/Visat)
Ro1 out+ 0 {1/(2*Go)}
Ro2 out- 0 {1/(2*Go)}
.ends


* Diff. transconductor with strong inversion V/I characteristics:
* ---------------------------------------------------------------
.subckt OTAmos in+ in- out+ out-
* Parameters:
* Gm=10u	; transconductance                              [A/V]
* A0=1000	; Dc gain	                                [A/V]
* Visat=100m	; input saturation voltage                        [A]
* Vios=0	; equivalent input offset                         [V]
* Cid=10f	; differential input parasitic capacitance        [F]
* Cic=10f	; input parasitic capacitance to ground           [F]
* fp=0		; parasitic frequency				  [s]
* gamma=2	; broadband noise factor (gm*Rni)                 [-]

.param Go={Gm/A0}
.param fp=100MEG
.param flow=1m
.param Vnat1Hz={sqrt(16E-21*fcorner*gamma/Gm)}

Cid in+ in- {Cid}
Cic1 in+ 0 {Cic}
Cic2 in- 0 {Cic}
Btp 0 1 I=Gm/gamma*(V(in+,in-)-Vios)
Rn 1 0 {gamma/Gm}
Cp 1 0 {Gm/(2*pi*fp*gamma)}
Xnf 1 2 Vnf Vnat1Hz={Vnat1Hz} flow={flow}
Bdm out- out+ I=Gm*limit(V(2),-sqrt(2)*Visat, sqrt(2)*Visat)*sqrt(1-min(V(2)*V(2)/Visat/Visat/4,0.5))
Ro1 out+ 0 {1/(2*Go)}
Ro2 out- 0 {1/(2*Go)}
.ends

* Filter library
* --------------

* Active-RC implementation
* ------------------------

* First-order LP section
* ----------------------
.subckt aRC1st in out
* Parameters:
* T0=1		; dc gain
* fc=1k		; cut-off frequency
* R=1k		; denormalization resistance
* A0=100k	; OPAMP dc gain

.param wc={2*pi*fc} R1={R} R2={R1/abs(T0)} C2={1/(R2*wc)}

R1 in 1 {R1}
R2 out 1 {R2}
C2 out 1 {C2}
E1 out 0 0 1 {A0}
.ends aRC1st

* Second-order LP Sallen-Key section
* ----------------------------------
.subckt aRCLPSK in out
* Parameters:
* f0=1k		; resonance frequency
* Q=1		; quality factor
* R=1k		; denormalization resistance
* A0=100k	; OPAMP dc gain

.param w0={2*pi*f0} R1={R} R2={R} C1={2*Q/(R*w0)} C2={1/(2*Q*R*w0)}

R1 in 1 {R1}
R2 1 2 {R2}
C1 1 out {C1}
C2 2 0 {C2}
E1 out 0 2 out {A0}
.ends aRCLPSK

* Switched Capacitor Components
* -----------------------------

* Switched capacitor
* ------------------
.subckt SC 1p1 2p1 1p2 2p2
* Parameters:
* C=1p
* fs=1k
* D=0.5
.param Req={1/(fs*C)} Gmeq={1/Req}
R1 1p1 2p1 {Req}
G1 2p1 1p1 1p2 2p2 Laplace=Gmeq*exp(-s*D/fs)
R2 1p2 2p2 {Req}
G2 2p2 1p2 1p1 2p1 Laplace=Gmeq*exp(-s*(1-D)/fs)
.ends SC

* Switched ideal OpAmp
* --------------------
.subckt SOPAMP in+p1 in-p1 outp1 in+p2 in-p2 outp2
* Parameters:
* Av=1E5
E1 outp1 0 in+p1 in-p1 {Av}
E2 outp2 0 in+p2 in-p2 {Av}
.ends SOPAMP

* Switched capacitor with one grounded node
* -----------------------------------------
.subckt SCG 1p1 1p2 gnd
* Parameters:
* C=1p
* fs=1k
* D=0.5
.param Req={1/(fs*C)} Gmeq={1/Req}
R1 1p1 gnd {Req}
G1 gnd 1p1 1p2 gnd Laplace=Gmeq*exp(-s*D/fs)
R2 1p2 gnd {Req}
G2 gnd 1p2 1p1 gnd Laplace=Gmeq*exp(-s*(1-D)/fs)
.ends SCG

* Switched ideal OpAmp with grounded positive input
* -------------------------------------------------
.subckt SOPAMPG in+ in-p1 outp1 in-p2 outp2
* Parameters:
* Av=1E5
E1 outp1 0 in+ in-p1 {Av}
E2 outp2 0 in+ in-p2 {Av}
.ends SOPAMPG