* LP passive RC circuit

* Continuous-time LP RC circuit
Vin in 0 DC 0 AC 1
R1 in outct {R}
C2 outct 0 {C2}
* SC circuit with delay approximation
XSC1 in 1 0 SCG C_val={C1} fs_val={fs} D_val={D}
XSC2 outsc 1 0 SCG C_val={C2} fs_val={fs} D_val={D}

.subckt SCG 1p1 1p2 gnd C_val=1p fs_val=1MEG D_val=0.5
* SC capacitors of value C_val with sampling frequency fs_val
* The circuit uses a transconductor of value Gmeq and delayed by DT=D*T
* Because LAPLACE is not available in ngspice we use XSpice s_xfer
* The latter implements a transfer function corresponding to a rational function in s
* The function below implements a 5th-order approximation of a constant delay exp(-s*DT)
.param Req={1/(fs_val*C_val)} Gmeq={1/Req}
.model delay1 s_xfer (
+ gain = {Gmeq}
+ int_ic = [0 0 0] 
+ num_coeff = [-1 30 -420 3360 -15120 30240]
+ den_coeff = [1 30 420 3360 15120 30240]
+ denormalized_freq={fs_val/D_val} )
.model delay2 s_xfer (
+ gain = {Gmeq}
+ int_ic = [0 0 0] 
+ num_coeff = [-1 30 -420 3360 -15120 30240]
+ den_coeff = [1 30 420 3360 15120 30240]
+ denormalized_freq={fs_val/(1-D_val)} )
R1 1p1 gnd {Req}
A1 %vd 1p2 gnd %id gnd 1p1 delay1
R2 1p2 gnd {Req}
A2 %vd 1p1 gnd %id gnd 1p2 delay2
.ends SCG

